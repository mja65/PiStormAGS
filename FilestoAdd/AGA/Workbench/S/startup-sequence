; Loads PeterK's icon library

Remlib >NIL: icon.library

; Check and install setpatch

FailAt 21
C:Version >NIL: exec.library 45 20
If WARN
  C:SetPatch QUIET
Else
  C:Version >NIL: C:SetPatch 44 16
  If WARN
    C:SetPatch QUIET
  Else
    C:SetPatch NOROMUPDATE QUIET
  EndIf
EndIf

Stack 16384

C:Version >NIL:

; Uncomment if you have a disk drive installed.

;C:AddBuffers >NIL: DF0: 15

C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
C:Copy >NIL: ENVARC: RAM:ENV ALL NOREQ

Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE

Assign >NIL: ENV: RAM:ENV
Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER

; Installs the PiStorm SHARED drive DOS driver.
; You need to set ENVARC:PiStorm to 1

If $PiStorm EQ "1"

 If $FirstBoot EQ "1"
  Delete >NIL: Libs:68020.library 
  Delete >NIL: Libs:68030.library 
  Delete >NIL: Libs:68040.library 
  Delete >NIL: Libs:68060.library 
  Delete >NIL: SYS:Prefs/Env-Archive/FirstBoot
  endif 

 DevEx >NIL: brcm-emmc.device
 If NOT WARN
  Copy SYS:Storage/DOSDrivers/PC0_Pistorm_emmc DEVS:DOSDrivers/PCO
  Copy SYS:Storage/DOSDrivers/PC0_Pistorm_emmc.info DEVS:DOSDrivers/PCO.info
 Else
  Copy SYS:Storage/DOSDrivers/PC0_Pistorm_sdhc DEVS:DOSDrivers/PCO
  Copy SYS:Storage/DOSDrivers/PC0_Pistorm_sdhc.info DEVS:DOSDrivers/PCO.info
 Endif
Endif

C:Mount >NIL: DEVS:DOSDrivers/~(#?.info)

IF EXISTS DEVS:Monitors
  IF EXISTS DEVS:Monitors/VGAOnly
    DEVS:Monitors/VGAOnly
  EndIF
  C:List >NIL: DEVS:Monitors/~(#?.info|VGAOnly) TO T:M LFORMAT "DEVS:Monitors/%s"
  Execute T:M
  C:Delete >NIL: T:M
EndIF

BindDrivers

SetEnv Workbench $Workbench
SetEnv Kickstart $Kickstart
UnSet Workbench
UnSet Kickstart

C:AddDataTypes REFRESH QUIET

IF "`C:Avail TOTAL`" GE "1250000" VAL
  C:LoadResource LIBS:diskfont.library LIBS:iffparse.library LIBS:asl.library LIBS:commodities.library
  C:IPrefs
  IF NOT $Language EQ "english"
    CD LOCALE:Catalogs/$Language
    C:LoadResource Sys/workbench.catalog Sys/libs.catalog Sys/prefs.catalog Sys/commodities.catalog Sys/utilities.catalog
    CD SYS:
  EndIF
Else
  C:IPrefs
EndIF

C:ConClip

; Remove if you install RTG

;C:FBlit
;C:FText
;C:BlazeWCP
;C:BBlank
;C:NoClick

C:PatchWB
C:FPPrefs

Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities

IF EXISTS S:User-Startup
  Execute S:User-Startup
EndIF

Resident Execute REMOVE
Resident Assign REMOVE

LoadWB
EndCLI