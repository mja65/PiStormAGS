failat 21

Unset Action
Set Action "Connect"
Unset Error

IF NOT EXISTS SYS:Prefs/Env-Archive/sys/wireless.prefs
 echo "Cannot connect to Wifi! No Wireless.prefs file found! You need to create a wireless.prefs file at *"SYS:Prefs/Env-Archive/sys*""
 Set Error "1"
 skip end
ENDIF

unset wirelessmanagerPID
set wirelessmanagerPID `status com=wirelessmanager`
if VAL $wirelessmanagerPID GT 0
 echo "Quiting Wireless Manager"
 BREAK $wirelessmanagerPID
ENDIF

echo "Connecting to Wireless. This may take a few moments......."
run >NIL: wirelessmanager device=Sys:Devs/Networks/wifipi.device CONFIG=sys:Prefs/Env-Archive/sys/wireless.prefs VERBOSE >RAM:wirelessmanagerlog.txt
waituntilconnected device=Sys:Devs/Networks/wifipi.device Unit=0 delay=100
if not warn
 echo "Connected to Wireless"
else
 echo "Could not connect to Wireless! Log is stored in RAM:wirelessmanagerlog.txt"
 Set Error "1"
endif

Lab end
unset Action
If $Error EQ "1"
 unset wirelessmanagerPID
 set wirelessmanagerPID `status com=wirelessmanager`
 if VAL $wirelessmanagerPID GT 0
  echo "Quiting Wireless Manager"
  BREAK $wirelessmanagerPID
 ENDIF
Endif
Unset Error
failat 10